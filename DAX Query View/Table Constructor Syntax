/*
Source: https://www.sqlbi.com/articles/using-tuple-syntax-in-dax-expressions/

The DAX language has a table constructor syntax to create an unnamed table in a DAX expression. 
This syntax is helpful when you need a temporary table with a small number of rows in a DAX 
expression or when you want to create a calculated table with constant and calculated values.

The more common use case for a table constructor is with the IN operator:
*/
EVALUATE
CALCULATETABLE (
    'Product',
    'Product'[Color] IN { "Red", "Blue", "White" }
)



/*
The previous example uses a table constructor that returns one column and three rows. 
However, the table constructor can have two or more columns for each row, as in the following example:
*/
EVALUATE
CALCULATETABLE (
    'Product',
    ( 'Product'[Color], 'Product'[Brand] )
        IN { ( "Red", "Contoso" ), ( "Blue", "Litware" ) }
)



/*
Suppose you need a small table with constant values. 
In that case, the calculated table with the table constructor is an alternative to the “Enter data” 
feature in Power BI, which generates a Power Query script with constant values:
*/
Price Range =
SELECTCOLUMNS (
    {
        ( 1, "LOW", 0, 100 ),
        ( 2, "MEDIUM", 100, 500 ),
        ( 3, "HIGH", 500, 9999999 )
    },
    "PriceRangeKey", [Value1],
    "Price Range", [Value2],
    "Min", [Value3],
    "Max", [Value4]
)



/*
An expression I originally used in the SM_COI model.
This DAX expression uses CALCULATETABLE in conjunction with SELECTCOLUMNS to create a filtered projection of the 'MIDP_DIRECTORY_G' table. The logic proceeds as follows:

Column Selection:
It extracts a subset of columns from the source table:
- "FULL_NAME": Employee’s full name
- "NETID": Unique network identifier
- "PREFERRED_FIRST_NAME": Chosen first name for display
- "PROPER_NAME_EMAIL": Formatted name associated with email

Row Filtering:
Only includes rows where the field 'CC_email_flag_expired' evaluates to TRUE(). This condition identifies directory records whose email flags are considered expired.

Overall Purpose:
The resulting table serves as a curated list of users with expired disclosures, containing just the essential identification and communication fields. This is used for downstream notifications.
*/

EVALUATE
CALCULATETABLE(
    SELECTCOLUMNS(
        'MIDP_DIRECTORY_G',
        "FULL_NAME", 'MIDP_DIRECTORY_G'[FULL_NAME],
        "NETID", 'MIDP_DIRECTORY_G'[NETID],
        "PREFERRED_FIRST_NAME", 'MIDP_DIRECTORY_G'[PREFERRED_FIRST_NAME],
        "PROPER_NAME_EMAIL", 'MIDP_DIRECTORY_G'[PROPER_NAME_EMAIL]
    ),
    'MIDP_DIRECTORY_G'[CC_email_flag_expired] = TRUE()
)