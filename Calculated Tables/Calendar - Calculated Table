// Create a dynamic calendar table using a calculated table (processed at the time of refresh).
// Update the first VAR (_calendar) with the proper table and date field names. Modify the 
// beginning dates as needed. i.e. - change to ", 7, 1 )" if you would like it to start July 1 instead of Jan 1.

Calendar = 

VAR _calendar =
CALENDAR(
        DATE ( YEAR ( MIN ( 'Table'[date_field] ) ), 1, 1 ),  --Modify here
        DATE ( YEAR ( MAX ( 'Table'[date_field] ) ), 12, 31 ) -- Modify here. everything else will automatically calculate.
    )
RETURN
ADDCOLUMNS(
    _calendar,
    "year", YEAR( [Date] ),
    "month_number", MONTH ( [Date] ),
    "month", FORMAT ( [Date], "mmm" ),
    "month_year_number", FORMAT ( [Date], "yy mm" ),
    "month_year", FORMAT ( [Date], "mmm yyyy" ),
    "weekday_number", WEEKDAY ( [Date], 2 ),
    "weekday", FORMAT ( [Date], "ddd" ),
    "fiscal_year", 
        VAR YearStartMonth = 7
        VAR YearEndMonth = 6
        VAR CurrentMonth = MONTH ( [Date] )
        VAR CurrentYear = YEAR ( [Date] )
        VAR FiscalYearStart = IF ( CurrentMonth >= YearStartMonth, CurrentYear, CurrentYear - 1 )
        VAR FiscalYearEnd = FiscalYearStart + 1
        RETURN
            FiscalYearEnd,
            //FORMAT (FiscalYearStart, "00" ) & "-" & FORMAT ( FiscalYearEnd, "00" ),
    "is_weekend", IF ( WEEKDAY ( [Date], 2 ) > 5, 1, 0 ),
    "fiscal_month", FORMAT ( [DATE], "mmm"),
    "fiscal_month_number",
            VAR CurrentMonth = MONTH ( [Date] )
            RETURN
                IF ( CurrentMonth >= 7, CurrentMonth - 6, CurrentMonth + 6 ),
    "last_5_and_current_fy",
        VAR CurrentDate = TODAY()
        VAR CurrentFY = IF ( MONTH( CurrentDate ) >= 7, YEAR( CurrentDate ) + 1, YEAR( CurrentDate ) )
        VAR DateFY = IF ( MONTH( [Date] ) >= 7, YEAR( [Date] ) + 1, YEAR( [Date] ) )
        RETURN
            IF (
                DateFY >= CurrentFY - 5 && DateFY <= CurrentFY,
                "True",
                "False"
            )
)